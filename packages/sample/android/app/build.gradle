apply plugin: 'com.android.application'
apply plugin: 're.notifica.gradle.notificare-services'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.huawei.agconnect'

def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "re.notifica.sample.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "3.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
            // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
            // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }

        manifestPlaceholders = [
                googleMapsApiKey       : properties.getProperty('google.maps.key'),
                notificareApplicationId: properties.getProperty('notificare.application.id')
        ]
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword properties.getProperty('keystore.debug.store.password')
            keyAlias properties.getProperty('keystore.debug.key.alias')
            keyPassword properties.getProperty('keystore.debug.key.password')
        }
        release {
            storeFile file('release.keystore')
            storePassword properties.getProperty('keystore.release.store.password')
            keyAlias properties.getProperty('keystore.release.key.alias')
            keyPassword properties.getProperty('keystore.release.key.password')
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir {
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }

    maven { url 'https://maven.notifica.re/releases' }
    maven { url 'https://maven.notifica.re/prereleases' }
    maven { url 'https://developer.huawei.com/repo' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'
